;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ banked                                            ³
;ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
;³                                                   ³
;³ copyright (C) 2017 jeff panici                    ³
;³ https://nybbles.io                                ³
;³                                                   ³
;³                                                   ³
;³                                                   ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ constants                                         ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ variables                                         ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
tiled_pal_idx   db  0
tiled_pal_color db  0

palette_label:
    s$_def  'PAL:'

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ btn_pal_prev_cb                                   ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
btn_pal_prev_cb:
    cmp     tiled_pal_idx, 0
    je      >l0
    dec     tiled_pal_idx
l0: ret

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ btn_pal_next_cb                                   ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
btn_pal_next_cb:
    cmp     tiled_pal_idx, 15
    je      >l0
    inc     tiled_pal_idx
l0: ret

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ tile_draw                                         ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
tile_draw:
    struc [bp]
                dw ?
        tv_pos  dw ?
        tv_h    db ?
        tv_w    db ?
    ends

    mov     bp, sp
    cs mov  bx, tv_pos
    xchg    bh, bl
    cs mov  dl, tiled_pal_idx
    shl     dl, 4
    mov     ch, tv_h
    mov     cl, tv_w
    cs mov  es, back_buffer_ptr
l1: ds mov  al, [si]
    aam     16
    add     ah, dl
    es mov  [bx], ah
    inc     bl
    add     al, dl
    es mov  [bx], al
    inc     bl
    inc     si
    dec     cl
    jnz     l1
    mov     cl, tv_w
    inc     bh
    sub     bl, tv_w
    sub     bl, tv_w
    dec     ch
    jnz     l1
    ret

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ _tiled_shell                                      ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_tiled_shell:
    vg_str  micro_font, palette_label, 150 by 2, 7 by 0, 1
    s$_dec2 palette_number, tiled_pal_idx
    vg_str  micro_font, palette_number, 154 by 10, 7 by 0, 1
    vg_fbox 43 by 20, 128 by 128, 8 by 0
    vg_box  42 by 20, 128 by 128, 0 by 0
    mov     bh, 238
    mov     bl, 0
    mov     dl, tiled_pal_idx
    mov     dh, 0
    mov     cx, 16
l0: vg_fbox bx, 16 by 8, dx
    vg_box  bx, 15 by 7, 0 by 0
    push    bx
    sub     bh, 12
    add     bl, 2
    s$_dec2 palette_number, dh
    vg_str  micro_font, palette_number, bx, 7 by 0, 1
    pop     bx
    cmp     dh, tiled_pal_color
    jne     >l1
    vg_box  bx, 15 by 7, 7 by 0
l1: inc     dh
    add     bl, 10
    dec     cx
    cmp     cx, 0
    je      >l2
    jmp     l0
l2: ret

te_shell macro
    push    ax, bx, cx, dx
    call    _tiled_shell
    pop     dx, cx, bx, ax
#em

;ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
;³ _tiled_draw                                       ³
;ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_tiled_draw:
    mov     bp, sp
    mov     bh, 43
    mov     bl, 21
    cs mov  dh, b[bp + 4]
    mov     dl, dh
    cs mov  ch, b[bp + 2]
l0: cs mov  cl, b[bp + 2]
l1: ds mov  al, [si]
    aam     16
    push    cx
    cs mov  cl, tiled_pal_idx
    mov     ch, ah
    vg_fbox bx, dx, cx
    add     bh, dh
    inc     bh
    mov     ch, al
    vg_fbox bx, dx, cx
    add     bh, dh
    inc     bh
    pop     cx
    inc     si
    sub     cl, 2
    cmp     cl, 0
    jne     l1
    add     bl, dl
    inc     bl
    mov     bh, 43
    dec     ch
    cmp     ch, 0
    jnz     l0
    ret

te_draw macro
    push    bp, ax, bx, cx, dx
    push    #2, #1
    call    _tiled_draw
    add     sp, 4
    pop     dx, cx, bx, ax, bp
#em

